define wk_show_JSObject
  if $arg0->inherits(&WebCore::JSNode::s_info)
    printf "NODE      "
    print WebCore::toNode($arg0)->nodeType()
    printf "          "
  else
    if $arg0->inherits(&WebCore::JSSVGElementInstance::s_info)
      printf "SVGElementInstance "
      print WebCore::toSVGElementInstance($arg0)
      printf "          "
    else
      printf "OBJECT    "
    end
  end

  print $arg0
end
document wk_show_JSObject
dump a JSObject
end

define wk_show_JSCell
  if $arg0->isObject()
    wk_show_JSObject (JSObject*)$arg0
  else
    if $arg0->inherits(&JSC::Structure::s_info)
      printf "STRUCTURE "
    else
      printf "????      "
    end

    print $arg0
  end
end
document wk_show_JSCell
dump a JSCell
end

define wk_show_ConservativeRoots
  printf "ConservativeRoots has %d roots\n", $arg0.size()
  set $wkscr_i = 0
  while $wkscr_i < $arg0.size()
    wk_show_JSCell $arg0.roots()[$wkscr_i]
    set $wkscr_i = $wkscr_i + 1
  end
end
document wk_show_ConservativeRoots
dump the pointers in ConservativeRoots
end
